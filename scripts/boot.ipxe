#!ipxe

# Variables are specified in boot.ipxe.cfg
set menu-default reboot

# Some menu defaults
set menu-timeout 600000
set submenu-timeout ${menu-timeout}
isset ${menu-default} || set menu-default exit

console --x 1024 --y 768 --picture ${git-uri}/images/image3.png --depth 32
cpair --foreground 7 --background 4 0

# Figure out if client is 64-bit capable
cpuid --ext 29 && set arch x64 || set arch x86
cpuid --ext 29 && set archb 64 || set archb 32
cpuid --ext 29 && set archl amd64 || set archl i386

###################### MAIN MENU ####################################

:start
menu iPXE boot menu
item --gap --             ------------------------- Windows Pre Boot Environment ----
item --key m MDT          Boot (M)DT from HTTP
item --key p PSD          Boot (P)SD from HTTP
item --key o OSDCloud     Boot (O)SDCloud from HTTP
item
item --gap --             ------------------------- StratoDesk NoTouchOS ------------
item --key n StratoDesk   Install StratoDesk (N)oTouchOS
item
item --gap --             ------------------------- Misc ----------------------------
item --key z nbxyz        Netboot.xy(z)
item shell                Drop to iPXE shell
item reboot               Reboot computer
item
item --key x exit         Exit iPXE and continue BIOS boot
choose --timeout ${menu-timeout} --default ${menu-default} selected || goto cancel
set menu-timeout 0
goto ${selected}

:StratoDesk
chain --autofree ${git-uri}/scripts/notouchos/menu.ipxe  || goto failed
goto start

:MDT
chain --autofree ${git-uri}/scripts/winpe/mdt.ipxe  || goto failed
goto start

:PSD
chain --autofree ${git-uri}/scripts/winpe/psd.ipxe  || goto failed
goto start

:OSDCloud
chain --autofree ${git-uri}/scripts/winpe/osdcloud.ipxe  || goto failed
goto start

:nbxyz
chain --autofree https://boot.netboot.xyz

:cancel
echo You cancelled the menu, dropping you to a shell

:shell
echo Type 'exit' to get the back to the menu
shell
set menu-timeout 0
set submenu-timeout 0
goto start

:failed
echo Booting failed, going back to menu
chain --autofree ${git-uri}/scripts/boot.ipxe

:reboot
reboot

:exit
exit
